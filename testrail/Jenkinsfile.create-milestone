pipeline {
    agent any

    parameters {
        string(name: 'RELEASE_NAME', description: 'Release name (e.g., Build Validation sign-off - Firefox RC 145.0)')
        string(name: 'RELEASE_TAG', description: 'Release tag (e.g., firefox-v145.0)')
    }

    options {
        timestamps()
    }

    environment {
        PATH = "/usr/local/bin:/usr/bin:/bin:/usr/sbin:/sbin:/opt/homebrew/bin:${env.PATH}"
        TESTRAIL_HOST = credentials('testrail-host')
        SLACK_MOBILE_TESTENG_RELEASE_CHANNEL = credentials('slack-mobile-testeng-webhook')
        SLACK_MOBILE_ALERTS_IOS_CHANNEL = credentials('slack-mobile-alerts-ios-webhook')
    }

    stages {
        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Setup Python') {
            steps {
                sh '''
                    set -e
                    echo "Checking Python installation..."
                    python3 --version
                    echo "Checking pip installation..."
                    python3 -m pip --version
                '''
            }
        }

        stage('Install Dependencies') {
            steps {
                dir('testrail') {
                    sh 'pip3 install --no-cache-dir -r requirements.txt'
                }
            }
        }

        stage('Create TestRail Credentials') {
            steps {
                script {
                    withCredentials([usernamePassword(credentialsId: 'testrail-credentials',
                                                      usernameVariable: 'TESTRAIL_USERNAME',
                                                      passwordVariable: 'TESTRAIL_PASSWORD')]) {
                        dir('testrail') {
                            sh '''
                                set -e
                                cat > .testrail_credentials.json <<EOF
{
  "host": "${TESTRAIL_HOST}",
  "username": "${TESTRAIL_USERNAME}",
  "password": "${TESTRAIL_PASSWORD}"
}
EOF
                                echo "Credentials file created successfully"
                                ls -la .testrail_credentials.json
                            '''
                        }
                    }
                }
            }
        }

        stage('Create Milestone') {
            steps {
                dir('testrail') {
                    script {
                        def result = sh(script: 'python3 testrail_main_ios.py', returnStatus: true)
                        if (result != 0) {
                            error("Failed to create milestone - Python script exited with code ${result}")
                        }
                    }
                }
            }
        }
    }

    post {
        always {
            dir('testrail') {
                sh 'rm -f .testrail_credentials.json'
            }
        }
        success {
            echo "Successfully created milestone for ${params.RELEASE_NAME}"
        }
        failure {
            echo "Failed to create milestone for ${params.RELEASE_NAME}"
        }
        cleanup {
            cleanWs()
        }
    }
}
